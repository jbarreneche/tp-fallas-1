define Region {
    clima : null,
    variabilidadTemp : null
}

define Edificio {
    desarrollo : null,
    pisos : null
}

define Vivienda {
    tamanio : null
}

define Infraestructura {
    gas : null,
    electricidad : null,
    vapor : null,
    combustible : null
}

define Sistema {
    nombre : null,
    tipo : null,
    distribucion : null,
    velocidadRegulacion : null,
    costoInstalacion : null,
    costoMantenimiento : null,
    permiteZonificacion : null
}

rule ClimaFrio {
    when {
        r : Region r.clima == 'frio';
        s : Sistema isUndefinedOrNull(s.tipo);
    }
    then {
        modify(s, function() { this.tipo = 'calor' })
    }
}

rule ClimaCalido {
    when {
        r : Region r.clima == 'calido';
        s : Sistema isUndefinedOrNull(s.tipo);
    }
    then {
        modify(s, function() { this.tipo = 'frio' })
    }
}

rule TemperaturaVariable {
    when {
        r : Region r.variabilidadTemp == 'alta';
        s : Sistema isUndefinedOrNull(s.velocidadRegulacion);
    }
    then {
        modify(s, function() { this.velocidadRegulacion = 'rapida' })
    }
}

rule SistemaGas {
    when {
        v : Vivienda v.tamanio != 'muy grande';
        i : Infraestructura isTrue(i.gas);
        s : Sistema isUndefinedOrNull(s.nombre) && s.tipo == 'calor';
    }
    then {
        modify(s, function() {
            this.nombre = 'Estufa de gas';
            this.distribucion = 'individual';
            this.permiteZonificacion = 'si';
        })
    }
}

rule SistemaVapor {
    when {
        i : Infraestructura isTrue(i.vapor);
        s : Sistema isUndefinedOrNull(s.nombre) && s.tipo == 'calor';
    }
    then {
        modify(s, function() {
            this.nombre = 'Calefacción por vapor';
            this.distribucion = 'central';
            this.costoDeInstalacion = 'alto';
        })
    }
}

rule SistemaPaneles {
    when {
        i : Infraestructura isTrue(i.gas) || isTrue(i.combustible);
        s : Sistema isUndefinedOrNull(s.nombre) && s.velocidadRegulacion != 'rapida'
            && s.tipo == 'calor';
    }
    then {
        modify(s, function() {
            this.nombre = 'Calefacción por paneles radiantes';
            this.distribucion = 'central';
            this.velocidadRegulacion = 'baja';
            this.permiteZonificacion = 'si';
            this.costoInstalacion = 'medio';
            this.costoMantenimiento = 'medio';
        })
    }
}

rule SistemaSplit {
    when {
        v : Vivienda isString(v.tamanio) && v.tamanio != 'muy grande';
        r : Region r.clima != 'frio';
        i : Infraestructura isTrue(i.electricidad);
        s : Sistema isUndefinedOrNull(s.nombre);
    }
    then {
        modify(s, function() {
            this.nombre = 'Split';
            this.distribucion = 'individual';
            this.permiteZonificacion = 'si';
        })
    }
}

rule SistemaCompacto1 {
    when {
        v : Vivienda v.tamanio == 'muy grande';
        i : Infraestructura isTrue(i.gas);
        s : Sistema isUndefinedOrNull(s.nombre) && s.tipo == 'calor';
    }
    then {
        modify(s, function() { this.nombre = 'Compacto' })
    }
}

rule SistemaCompacto2 {
    when {
        v : Vivienda v.tamanio == 'muy grande';
        r : Region r.clima != 'frio';
        i : Infraestructura isTrue(i.gas) && isTrue(i.electricidad);
        s : Sistema isUndefinedOrNull(s.nombre);
    }
    then {
        modify(s, function() { this.nombre = 'Compacto' })
    }
}

rule SistemaCompacto3 {
    when {
        e : Edificio e.desarrollo == 'horizontal';
        i : Infraestructura isTrue(i.gas);
        s : Sistema isUndefinedOrNull(s.nombre) && s.tipo == 'calor';
    }
    then {
        modify(s, function() { this.nombre = 'Compacto' })
    }
}

rule SistemaCompacto4 {
    when {
        e : Edificio e.desarrollo == 'horizontal';
        r : Region r.clima != 'frio';
        i : Infraestructura isTrue(i.gas) && isTrue(i.electricidad);
        s : Sistema isUndefinedOrNull(s.nombre);
    }
    then {
        modify(s, function() { this.nombre = 'Compacto' })
    }
}

rule SistemaCompacto {
    when {
        s : Sistema s.nombre == 'Compacto' && isUndefinedOrNull(s.distribucion);
    }
    then {
        modify(s, function() {
            this.distribucion = 'mixta';
            this.permiteZonificacion = 'si';
            this.costoInstalacion = 'medio';
            this.costoMantenimiento = 'medio';
        });
    }
}
rule SistemaAguaCaliente {
    when {
        e : Edificio;
        i : Infraestructura isTrue(i.gas) && isTrue(i.electricidad);
        r : Region r.clima == 'templado';
        s : Sistema isUndefinedOrNull(s.nombre);
    }
    then {
        modify(s, function() {
            this.nombre = 'Calefacción por agua caliente';
            this.tipo = 'calor';
            this.distribucion = 'central';
            this.permiteZonificacion = 'si';
            this.costoDeInstalacion = 'medio';
            this.costoMantenimiento = 'medio';
        });
    }
}

rule SistemaTodoAire {
    when {
        e : Edificio e.desarrollo == 'horizontal';
        r : Region r.clima != 'frio';
        i : Infraestructura isTrue(i.electricidad);
        s : Sistema isUndefinedOrNull(s.nombre);
    }
    then {
        modify(s, function() {
            this.nombre = 'Todo Aire';
            this.distribucion = 'central';
            this.permiteZonificacion = 'no';
            this.costoDeInstalacion = 'medio';
            this.costoMantenimiento = 'bajo';
        });
    }
}

rule SistemaVRV {
    when {
        e : Edificio e.desarrollo == 'vertical' && e.pisos >= 30;
        i : Infraestructura isTrue(i.electricidad);
        s : Sistema isUndefinedOrNull(s.nombre);
    }
    then {
        modify(s, function() {
            this.nombre = 'VRV';
            this.distribucion = 'mixto';
            this.permiteZonificacion = 'si';
            this.costoDeInstalacion = 'alto';
            this.costoMantenimiento = 'bajo';
        });
    }
}

rule SistemaFanCoil {
    when {
        e : Edificio e.desarrollo == 'vertical' && e.pisos < 30;
        i : Infraestructura isTrue(i.electricidad);
        s : Sistema isUndefinedOrNull(s.nombre);
    }
    then {
        modify(s, function() {
            this.nombre = 'Fan-Coil';
            this.distribucion = 'mixto';
            this.permiteZonificacion = 'si';
            this.costoDeInstalacion = 'medio';
            this.costoMantenimiento = 'medio';
        });
    }
}

